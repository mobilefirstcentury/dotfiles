---- Coffee Script ----
======================

Installation
------------
    npm install -g coffee-script # latest stable
    npm install -g jashkenas/coffeescript # latest master branch from coffee script repo


Usage
-----

  $ coffee  

  -b, --bare         compile without a top-level function wrapper
  -c, --compile      compile to JavaScript and save as .js files
  -e, --eval         pass a string from the command line as input
  -h, --help         display this help message
  -i, --interactive  run an interactive CoffeeScript REPL
  -j, --join         concatenate the source CoffeeScript before compiling
  -m, --map          generate source map and save as .js.map files
  -n, --nodes        print out the parse tree that the parser produces
      --nodejs       pass options directly to the "node" binary
      --no-header    suppress the "Generated by" header
  -o, --output       set the output directory for compiled JavaScript
  -p, --print        print out the compiled JavaScript
  -r, --require      require the given module before eval or REPL
  -s, --stdio        listen for and compile scripts over stdio
  -l, --literate     treat stdio as literate style coffee-script
  -t, --tokens       print out the tokens that the lexer/rewriter produce
  -v, --version      display the version number
  -w, --watch        watch scripts for changes and rerun commands


Functions
----------
  + fonction call : `→` means is converted in javascript code
  
    `console.log sys.inspect object` → `console.log(sys.inspect(object));`

  + definition

    ```coffee
    square = (x) -> x * x
    cube   = (x) -> square(x) * x
    ```
  + arguments default values

    ```coffee
    fill = (container, liquid = "coffee") ->
      "Filling the #{container} with #{liquid}..."
    ```
  + Coffee Scripts returns everything as often as possible (better for readability and usability) 
    ```coffee
    # In examples below we don't need to explicitely return the strings
    grade = (student) ->
      if student.excellentWork
        "A+"
      else if student.okayStuff
        if student.triedHard then "B" else "B-"
      else
        "C"

    eldest = if 24 > 21 then "Liz" else "Ike"
    ```

Objects and Arrays
------------------

    ```coffee
    song = ["do", "re", "mi", "fa", "so"]

    singers = {Jagger: "Rock", Elvis: "Roll"}

    bitlist = [
      1, 0, 1
      0, 0, 1
      1, 1, 0
    ]

    kids =
      brother:
        name: "Max"
        age:  11
      sister:
        name: "Ida"
        age:  9

    ages = max: 10, ida: 9, tim: 11

    # note: unlike javascript, reserved words can bu used as object keys normally
    $('.account').attr class: 'active'
    ```


Lexical Scoping and Variable Safety
----------------------------------

  - All of the variable declarations have been pushed up to the top of the closest scope.
  - Javascript generated code is protected in a `(function(){ ... })();` safety wrapper:
    + global scope can't be polluted
    + to create global variable, one has to attach them to **window** or **exports** objects:

    ```coffee
    root = exports ? this
    root.foo = -> 'Hello World'
    ```


If Else Unless, and Conditional Assignment
--------------------------------------------

    ```coffee
    mood = greatlyImproved if singing
    mood = joyfull unless today is monday

    if happy and knowsIt
      clapsHands()
      chaChaCha()
    else
      showIt()

    date = if friday then sue else jill
    ```


Splats
---------

    ```coffee
    gold = silver = rest = "unknown"

    awardMedals = (first, second, others...) ->
      gold   = first
      silver = second
      rest   = others

    contenders = [
      "Michael Phelps"
      "Liu Xiang"
      "Yao Ming"
      "Allyson Felix"
      "Shawn Johnson"
      "Roman Sebrle"
      "Guo Jingjing"
      "Tyson Gay"
      "Asafa Powell"
      "Usain Bolt"
    ]

    awardMedals contenders...

    alert "Gold: " + gold
    alert "Silver: " + silver
    alert "The Field: " + rest
    ```

Loops and Comprehensions
------------------------

### general notes
  - Coffee Script used smart loops ('arrays conprehensions'): `num for num in number-array`
  - **note** smart loops should replace most use cases of **each**/**forEach**, **map**, or **select**/**filter**, ...
  - unlike for loops, coffee script smart loops  are expressions, and can be returned and assigned.
  - if assigned to a variable, all iteration steps results are collected in an array (also true for while loops -- see below) 
  ```coffee
  # all global properties in a array
  globals = (name for name of window)[
  # The first ten global properties only.
  globals = (name for name of window)[0...10] 
  ```

### normal iterations
    ```coffee
    eat food for food in ['toast', 'cheese', 'wine']

    courses = ['greens', 'caviar', 'truffles', 'roast', 'cake']
    echo "choice #{i+1}: #{dish}" for dish, i in courses

    foods = ['broccoli', 'spinach', 'chocolate']
    eat food for food in foods when food isnt 'chocolate'

    shortNames = (name for name in list when name.length < 5)

    countdown = (num for num in [10..1])

    evens = (x for x in [0..10] by 2) 

    echo "this line is repeated 10 times" for [0...10]

    ```
### iteration over keys and values

    ```coffee
    yearsOld = max: 10, ida: 9, tim: 11

    ages = for child, age of yearsOld
      "#{child} is #{age}"
    ```
### iteration over non inherited keys 
    `for own key, value of object`

### while loop

    ```coffee
    buy()  while supply > demand
    buy()  until demand >= supply
    
    num = 6
    lyrics = while num -= 1
      "#{num} little monkeys, jumping on the bed.
        One fell out and bumped his head."


    # loop is a shortcut for `while true`
    loop 
      climb-one-step-to-the-sky()

    # there's no `do X while Y `loop, we have to emulate it
    loop 
      X
      break unless Y
    ```

### variable discrimination in callbacks embedded in loops
    + using callbacks in loops is REALLY UNINTUITIVE: callbacks access outside variables "by reference" (sort of) not "by value" 
    + so one ends frequently with all callback sharing last iterated value of the used variable: the following code bugs as filename is always `list[0]`
    ```coffee
    for (i = 0, len = 10; i < len; i++) {
      filename = list[i];
      fs.readFile(filename, function(err, contents) {
        return compile(filename, contents.toString());
      });
    }
    ```

    + the classic work around is to wrap the code with a function that's immediatly called and given used variables as arguments (arguments are passed by value)
    + CoffeeScript provides the `do` keyword for just that ...

    ```coffee
    for filename in list
      do (filename) ->
        fs.readFile filename, (err, contents) ->
          compile filename, contents.toString()
    ```


Array Slicing and Splicing with Ranges
--------------------------------------

### slicing

    ```coffee
    numbers = [0...10]  # means [0,1,2,3,4,5,6,7,8,9] as `...` excludes last element
    start   = numbers[0..2] # [ 0, 1, 2 ]
    middle  = numbers[3...-2]  # [ 3, 4, 5, 6, 7 ]
    end     = numbers[-2..] # [ 8, 9 ]
    copy    = numbers[..]  # [0,1,2,3,4,5,6,7,8,9]
    ```

### array assignement (splicing) 
  **note** JavaScript strings are immutable, and can't be spliced.
    numbers = [0,...10]
    numbers[3..6] = [-3, -4, -5, -6]





Operators and Aliases
---------------------
### Coffee Script / Javascript equivalences

 CoffeeScript  | JavaScript
---            | --
==             | ===
!=             | !==
is             | ===
isnt           | !==
not            | !
and            | &&
or             | ||
true, yes, on  | true
false, no, off | false
@, this        | this
of             | in (present in object keys)
in             | no JS equivalent (present in array values)
a ** b         | Math.pow(a, b)
a // b         | Math.floor(a / b)
a %% b         | (a % b + b) % b
then           | Instead of a newline or semicolon, `then` can be used to separate conditions from expressions, in **while**, **if**/**else**, and **switch**/**when** 
@property      | this.property

### Example idioms

    ```coffee
    launch() if ignition is on
    volume = 10 if band isnt SpinalTap
    letTheWildRumpusBegin() unless answer is no
    winner = yes if pick in [47, 92, 13]
    print inspect "My name is #{@name}"
    ```

?, The Existential Operator and Accessor
--------------------------------------

** note **
  Operator `?` returns true unless a variable is **null** or **undefined**

    ```coffee
    # test for the existence of mind
    solipsism = true if mind?

    # binary ? returns 'yeti' if it exists and 'bear' otherwise
    footprints = yeti ? "bear"

    # provide default value (better than javascript classic |=)
    # equivalent to speed = speed ? 15 
    # [TODO] but speed HAS to be already declared but i don't know how to declare in CoffeeScript
    speed ?= 15

    # call a function safely: nothing is executed but no error is thrown...
    noSuchFunction?()

    # if lottery has a drawWinner method and if the draw Winner has an adress then get the zip code 
    zip = lottery.drawWinner?().address?.zipcode
    ```


Classes, Inheritance, and Super
-------------------------------

### Simple Class delcaration and inheritance example
    class Animal
      constructor: (@name) ->

      move: (meters) ->
        alert @name + " moved #{meters}m."

    class Snake extends Animal
      move: ->
        alert "Slithering..."
        super 5

    class Horse extends Animal
      move: ->
        alert "Galloping..."
        super 45

    sam = new Snake "Sammy the Python"
    tom = new Horse "Tommy the Palomino"

    sam.move()
    tom.move()

### extends, :: and super operators
  - extends can be used with any couple of 'à la javascript' constructors functions 
  - `::` operator gives quick access to an object's prototype.
  - `super` is converted into a call against the immediate ancestor's method of the same name.

    ```coffee
    String::dasherize = ->
      this.replace /_/g, "-"

    String.prototype.dasherize = function() {
      return this.replace(/_/g, "-");
    };
    ```

Destructuring Assignment
------------------------

### variable swapping
    ```coffee
    theBait   = 1000
    theSwitch = 0
    [theBait, theSwitch] = [theSwitch, theBait]
    ```

### Functions returning multiples values 

    ```coffee
    weatherReport = (location) ->
      # Make an Ajax request to fetch the weather...
      [location, 72, "Mostly Sunny"]

    [city, temp, forecast] = weatherReport "Berkeley, CA"
    ```


### nested properties pull out

    ```coffee
    # this translates to `spawn = require('child_process').spawn`
    {spawn} = require "child_process" 
    ```

    ```coffee
    futurists =
      sculptor: "Umberto Boccioni"
      painter:  "Vladimir Burliuk"
      poet:
        name:   "F.T. Marinetti"
        address: [
          "Via Roma 42R"
          "Bellagio, Italy 22021"
        ]

    {poet: {name, address: [street, city]}} = futurists

    ```
### With splats, destructuring on steroids

   ```coffee
   [first-letter, middle..., last-letter] = "impossible".split("")
   [first-word, ..., last-word] = "Every literary critic believes he will outwit history and have the last word".split " "
    ```

### Destructured Options for Class Constructors 
** note ** Best practice for class definition

    ```coffee
    class Person
      constructor: (options) ->
        {@name, @age, @height = 'average'} = options

    tim = new Person name: 'Tim', age: 4
    ```


Bound Functions, Generator Functions
------------------------------------

  - In JavaScript, to preserve `this` reference in callbacks one has to write weird boilerplate code. 
  - Coffee Scripts used the fat arrow operator to attach a callback to specific `this` object

    ```coffee
    # @customer and @cart are what we expect in the 'fat arrow-ed' callback
    # without the fat arrow `@customer` would have referred to the undefined "customer" property of the DOM element and would have raised an exception.
    Account = (customer, cart) ->
      @customer = customer
      @cart = cart

      $('.shopping_cart').on 'click', (event) =>
        @customer.purchase @cart
    ```


Generators
---------

    ```coffee
    perfectSquares = ->
      num = 0
      loop
        num += 1
        yield num * num
      return

    console.log perfectSquares.next().value
    console.log perfectSquares.next().value
    ```


Switch/When/Else
-----------------

    # For improved readability there's no need for 'break' operator in switch statement
    ```coffee
    switch day
      when "Mon" then go work
      when "Tue" then go relax
      when "Thu" then go iceFishing
      when "Fri", "Sat"
        if day is bingoDay
          go bingo
          go dancing
      when "Sun" then go church
      else go work
      ```
    # We can have a much cleaner if/else chain with switch operator
    ```coffee
    score = 76
    grade = switch
      when score < 60 then 'F'
      when score < 70 then 'D'
      when score < 80 then 'C'
      when score < 90 then 'B'
      else 'A'
    ```

Try/Catch/Finally
-------------------


    ```coffee
    try
      allHellBreaksLoose()
      catsAndDogsLivingTogether()
    catch error
      print error
    finally
      cleanUp()
    ```

Chained Comparisons
-------------------

    ```coffee
    cholesterol = 127
    healthy = 200 > cholesterol > 60
    ```


String Interpolation, Block Strings, and Block Comments
-------------------------------------------------------

### Ruby-style string interpolation

  - double quoted strings allow for interpolated values
  - single quoted strings are literals

    ```coffee
    author = "Wittgenstein"
    quote  = "A picture is a fact. -- #{ author }"

    sentence = "#{ 22 / 7 } is a decent approximation of π"
    ```

### multi-line strings 

    # front indentations are ignored
    mobyDick = "Call me Ishmael. Some years ago,
      never mind how long precisely, having little
      or no money in my purse, and nothing particular
      to interest me on shore, I thought I would sail
      about a little and see the watery part of the
      world..."

### block strings (with smart indentation)

    # first identation is preserved to have code aligned
    ```coffee
    html = """
           <strong>
             cup of coffeescript
           </strong>
           """
    ```
    tranlslates in ...

    ```coffee
    html = "<strong>\n  cup of coffeescript\n</strong>";
    ```


Javascript comments generation
-------------------------------

    # this will generate comments in the compiled javascript code
    ```coffee
    ###
    SkinnyMochaHalfCaffScript Compiler v1.0
    Released under the MIT License
    ###
    ```


Block Regular Expressions
-------------------------
  - block regex are extended regular expressions that ignore internal whitespace and can contain comments and interpolation. 
  - modeled after Perl's `/x` modifier, CoffeeScript's block regexes are delimited by `///` 

    ```coffee
    OPERATOR = /// ^ (
      ?: [-=]>             # function
       | [-+*/%<>&|^!?=]=  # compound assign / compare
       | >>>=?             # zero-fill right shift
       | ([-+:])\1         # doubles
       | ([&|<>])\2=?      # logic / shift
       | \?\.              # soak access
       | \.{2,3}           # range or splat
    ) ///
    ```


Cake, and Cakefiles
-------------------
  **notes** [shortcake](https://github.com/zeekay/shortcake) is a much more powerfull alternative.
  
  - A very basic build system for coffee Script
  - to execute tasks:
    $ cake -o "myDir" setup

    # [TODO] not sure this code is correct ...
    ```coffee
    fs = require 'fs'
    dir = ""

    option '-o', '--output [DIR]', 'output directory'

    task 'setup', 'setup da thing', (options) ->
      dir  = options.output or 'lib'
      setup-details = ...
      fs.writeFile "#{dir}/setup-file.js", setup-details

    task 'run', 'run da thing', (options) ->
      invoke 'setup'
      # do something with `dir` and setup file ...
    ```

Micro examples
--------------

### Booleans have the value of (true, yes, or on) or (false, no, or off)
echo('areYouHappy is a Boolean<br>') if typeof areYouHappy is 'boolean'

### MATH
echo("5 + 2 = #{5 + 2}")
echo("5 - 2 = #{5 - 2})
echo("5 * 2 = #{5 * 2}")
echo("5 / 2 = #{5 / 2}")
echo("5 % 2 = #{5 % 2}")

### Coffee Script has 16 digits of precision
precisionTest = 0.1000000000000001;
echo("Precision : #{precisionTest + 0.10000000000000011}")


### Round to 2 digits
balance = 1563.87
echo("Monthly Payment = #{(balance/12).toFixed(2)}")


### Shortcut for adding 1
randNum = 5
echo("randNum++ = #{randNum++}")
echo("++randNum = #{++randNum}")

### Shortcut for subtracting 1
randNum = 5
echo("randNum-- = #{randNum--}")
echo("--randNum = #{--randNum}")

### Order of operations
echo("3 + 2 * 5   = #{3 + 2 * 5}")
echo("(3 + 2) * 5 = #{(3 + 2) * 5}")

### Math properties and functions
```coffee
echo("Math.E           = #{Math.E}")
echo("Math.PI          = #{Math.PI}")
echo("Math.abs(-8)     = #{Math.abs(-8)}")
echo("Math.cbrt(1000)  = #{Math.cbrt(1000)}")
echo("Math.ceil(6.45)  = #{Math.ceil(6.45)}")
echo("Math.floor(6.45) = #{Math.floor(6.45)}")
echo("Math.round(6.45) = #{Math.round(6.45)}")
echo("Math.log(10)     = #{Math.log(10)}")
echo("Math.log10(10)   = #{Math.log10(10)}")
echo("Math.pow(4,2)    = #{Math.pow(4,2)}")
echo("Math.sqrt(1000)  = #{Math.sqrt(1000)}")
```

### Generate a random number
```coffee
randNum = Math.floor(Math.random() * 100) + 1
echo("Random Number = #{randNum}")
```
### STRINGS 

```coffee
fName = "Derek"
lName = "Banas"
longString = "This is a long string we can play with"
```

#### You can combine Strings with a +

```coffee
echo(fName + " " + lName )
```


#### Get the String length
```coffee
echo("String Length : #{longString.length}")
```

#### Get the index of a matching string
```coffee
echo("Index of string : #{longString.indexOf("goes")}")
```

#### Get a character at an index
```coffee
echo("Index 8 : #{longString.charAt(8)}")
```

#### Get a substring
```coffee
echo("Word at 27 : #{longString.slice(27,31)}")
```

#### Get everything after an index
```coffee
echo("After 27 : #{longString.slice(27)}")
```

#### Replace a string with another
```coffee
longString = longString.replace("forever", "and on forever   ")
echo("New String : #{longString}")
```

#### Convert a string into an Array
```coffee
strArray = longString.split(" ")
```

#### Output an array
```coffee
for x in strArray
  echo("#{x}")
  ```

#### Trim whitespace
```coffee
longString = longString.trim()
```

#### Convert to all uppercase
```coffee
echo("#{longString.toUpperCase()}")
```

#### Convert to all lowercase
echo("#{longString.toLowerCase()}")

### CONDITIONALS 

#### if
```coffee
age = 3

if age >= 18
  echo('You can Vote')
  if (age >= 16)
    echo('You can Drive also>')
else if (age >= 16)
  echo('You can Drive')
else
  echo('You\'ll be 16 soon')
```

#### Unless executes if the test returns false

```coffee
unless (age >= 19)
  echo('You are in school')
else
  echo('You may go to college')
  ```

#### Logical operators are &&, || and !

```coffee
hsGrad = true

if !(age > 4) || (age > 65)
  echo('You don\'t go to school')
else if (age >= 5) && (age <= 6)
  echo('Go to Kindergarten')
else if (age > 6) && (age <= 18)
  schoolGrade = "Go to Grade #{age - 5}<br>"
    echo(schoolGrade)
else
  echo('Go to Work')
  ```

#### Ternary operator
```coffee
votingAge = if age > 18 then true else false
echo("Can Vote : #{votingAge}")
```

#### Switch

```coffee
childAge = 7

switch childAge
  when 5 then echo('Go to Kindergarten')
  when 6, 7, 8, 9, 10 then echo('Go to Elementary School')
  else echo('Go to Someplace')
```

#### Do something if the variable has a value
```coffee
if age?
  echo("#{age} years old")
```

#### The existential operator can also assign 1 value if it exists or another if not
```coffee
chicken = null
chickenName = chicken ? "Fred"
echo("Chickens Name : #{chickenName}")
```

#### We can stack conditions
```coffee
hat = "Winter Hat"
coat = "Winter Coat"
gloves = null
```

#### Only execute if we have a hat and coat
```coffee
if hat? and coat?
  echo('beforeend',
    "#{hat} #{coat} #{gloves ? '?'}<br>")
```

### ARRAYS

```coffee
randArray = ["word", false, 1234, 1.234]
echo("Index 2 : #{randArray[2]}")
```

#### Get the last 2 indexes
```coffee
echo("Last 2 : #{randArray[2..3]}")
```

#### Defines an array with a range from 1 to 10

```coffee
oneTo10 = [1..10]
```

#### You can go backwards as well

```coffee
tenTo1 = [10..1]
```

#### Combine Arrays
```coffee
combinedArray = oneTo10.concat tenTo1
```

#### Splat "soak up" a list of arguments, or all the values in the array
```coffee
oneTo10.push tenTo1...
```

#### Use a for loop to cycle through the array
```coffee
for x in oneTo10
  echo("#{x}")
  ```

#### Convert an array into a String
```coffee
echo("#{oneTo10.toString()}")
```

#### Filter out all odds by saving elements that return true for the condition
```coffee
evensOnly = oneTo10.filter (x) -> x % 2 == 0
echo("#{evensOnly.toString()}")
```

#### Get the maximum value in the array
```coffee
echo("Max : #{Math.max oneTo10...}")
```

#### Get the minimum value in the array
```coffee
echo("Min : #{Math.min oneTo10...}")
```

#### Sum items in an array
```coffee
sumOfArray = oneTo10.reduce (x,y) -> x+y
echo("Sum : #{sumOfArray}")
```

#### Reverse an Array
```coffee
echo("Reverse : #{tenTo1.reverse()}")
```

#### Create an array of objects
```coffee
peopleArray = [
  {
    name: "Paul"
    age: 43
  },
  {
    name: "Sue"
    age: 39
  },
]
```

#### Access item by key in array
```coffee
echo("First Name : #{peopleArray[0].name}")
```

### LOOPING

#### loop in an array
```coffee
for x in oneTo10
  echo("#{x}")
```

#### We can use the guard when to print out only odd numbers

```coffee
for x in oneTo10 when x%2 isnt 0
  echo("#{x}")
```

#### We can cycle trough a range of numbers and print out the evens
```coffee
for x in [50..100] when x%2 is 0
  echo("#{x}")
```

#### We can skip certain values using by
```coffee
for x in [20..40] by 2
  echo("#{x}")
```

#### We can access indexes

```coffee
employees = [
  "Doug"
  "Sue"
  "Paul"
]

for employee, employeeIndex in employees
  echo("Index: " + employeeIndex + " Employee: " + employee )
```

#### We can search for a value with in
```coffee
if "Doug" in employees
  echo("I Found Doug")
```

#### Let's count from 100 to 110 with a while loop
```coffee
i = 100

while (i += 1) <= 110
  echo("i = #{i}")
```

#### You can use a while loop to cycle until 0 is reached
```coffee
monkeys = 10
while monkeys -= 1
  echo("#{monkeys} little monkeys, jumping on the bed. One fell off and bumped his head.")
```

#### There is no Do While loop but it can be emulated
```coffee
x = 0
loop
  echo('beforeend', "#{++x}<br>")
  break unless x != 5
```

### FUNCTIONS

#### definition
```coffee
helloFunc = (name) ->
  return "Hello #{name}"

echo("#{helloFunc("Derek")}")
```

#### function with no attributes

```coffee
getRandNum = ->
  return Math.floor(Math.random() * 100) + 1

echo("Random Number : #{getRandNum()}")
```

#### You can receive an undefined number of values with vars...
```coffee
sumNums = (vars...) ->
  sum = 0
  for x in vars
    sum += x
  return sum

echo("Sum : #{sumNums(1,2,3,4,5)}")
```

#### The last expression in a function is returned by default 
```coffee
movieRank = (stars = 1) ->
  if stars <= 2
    "Bad"
  else
    "Good"

echo("Movie Rank : #{movieRank()}")
```

#### Recursive function that calculates a factorial
```coffee
factorial = (x) ->
  return 0 if x < 0
  return 1 if x == 0 or x == 1
  return x * factorial(x - 1)

echo("Factorial of 4 : #{factorial(4)}")
```

### OBJECTS

```coffee
derek = {name: "Derek", age: 41, street: "123 Main St"}

echo("Name : #{derek.name}")
```

#### How we add an object property
```coffee
derek.state = "Pennsylvania"
```

#### Cycle through an object
```coffee
for x, y of derek
  echo(x + " is " + y")
```

### CLASSES

```coffee
class Animal

  # List properties along with their default values
  name: "No Name"
  height: 0
  weight: 0
  sound: "No Sound"

  # Define a static property that is shared by all objects
  @numOfAnimals: 0

  # Static methods also start with @
  @getNumOfAnimals: () ->
    Animal.numOfAnimals

  # The constructor is called when the object is created
  # If we use @ with attributes the value is automatically assigned
  constructor: (name = "No Name", @height = 0, @weight = 0) ->

    # @ is like this in other languages
    @name = name

    # You access static properties using the class name
    Animal.numOfAnimals++

  # A class function
  makeSound: ->
    "says #{@sound}"

  # Use @ to reference the objects properties
  # Use @ to call ofther methods of this object
  getInfo: ->
    "#{@name} is #{@height} cm and weighs #{@weight} kg and #{@makeSound()}"
```

### Create an Animal object
```coffee
grover = new Animal()
```

### Assign values to the Animal object
```coffee
grover.name = "Grover"
grover.height = 60
grover.weight = 35
grover.sound = "Woof"

echo("#{grover.getInfo()}")
```

### You can attach new object methods outside of the class
```coffee
Animal::isItBig = ->
  if @height >= 45
    "Yes"
  else
    "No"

echo("Is Grover Big #{grover.isItBig()}")
echo("Number of Animals #{Animal.getNumOfAnimals()}")
```

### INHERITANCE

```coffee
class Dog extends Animal
  sound2: "No Sound"

  constructor: (name = "No Name", height = 0, weight = 0) ->

    # When super is called in the constructor CS calls the super classes
    # constructor
    super(name, height, weight)

  # You override methods by declaring one with the same name
  # CS is smart enough to know you are calling the Animal version
  # of makeSound when you just type super
  makeSound: ->
    super + " and #{@sound2}"

sparky = new Dog("Sparky")

sparky.sound = "Wooooof"
sparky.sound2 = "Grrrrr"

echo("#{sparky.getInfo()}")
```
